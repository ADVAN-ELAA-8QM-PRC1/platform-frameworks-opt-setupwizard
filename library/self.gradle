/**
 * This self.gradle build file is only run when built in ub-setupwizard-* branches.
 */

apply plugin: 'dist'

apply from: 'standalone-rules.gradle'

// Add targets for tests
android.sourceSets {
    androidTest {
        manifest.srcFile 'test/AndroidManifest.xml'
        java.srcDirs = ['test/src']
        res.srcDirs = ['test/res']

        dependencies {
            androidTestCompile 'com.android.support.test:rules:0.5'
            androidTestCompile 'com.android.support.test:runner:0.5'
            androidTestCompile 'junit:junit:4.+'
        }
    }

    androidTestEclairMr1Compat {
        java.srcDirs = ['eclair-mr1/test/src']
    }

    androidTestFullSupport {
        java.srcDirs = ['full-support/test/src', 'eclair-mr1/test/src']
        res.srcDirs = ['full-support/test/res']
    }
}
android.defaultConfig.testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

android.lintOptions {
    // TODO: Fix lint errors and re-enable fatal lint
    abortOnError false
    htmlReport true
    textOutput 'stderr'
    textReport true
    xmlReport false
}

// Run lint for all variants
android.libraryVariants.all { variant ->
    variant.assemble.dependsOn(tasks.findByName('lint'))
}

// Output all test APKs to the distribution folder
def distTask = tasks.findByName('dist')
if (distTask) {
    android.testVariants.all { variant ->
        // Make the dist task depend on the test variant, so the test APK will be built
        distTask.dependsOn variant.assemble
        // TODO: remap the different test variants to different file names
    }
}
