/**
 * This self.gradle build file is only run when built in ub-setupwizard-* branches.
 */

apply plugin: 'dist'

apply from: 'standalone-rules.gradle'
apply from: '../tools/gradle/docs.gradle'

task docs(dependsOn: 'javadocFullSupportRelease')

// Add targets for tests
android.sourceSets {
    androidTest {
        manifest.srcFile 'test/AndroidManifest.xml'
        java.srcDirs = ['test/src']
        res.srcDirs = ['test/res']
    }

    androidTestEclairMr1Compat {
        java.srcDirs = ['eclair-mr1/test/src']
    }

    androidTestFullSupport {
        java.srcDirs = ['full-support/test/src', 'eclair-mr1/test/src']
        res.srcDirs = ['full-support/test/res']
    }
}

android.lintOptions {
    abortOnError true
    htmlReport true
    textOutput 'stderr'
    textReport true
    xmlReport false
}

// Run lint for all variants
android.libraryVariants.all { variant ->
    variant.assemble.dependsOn(tasks.findByName('lint'))
}

// Output all test APKs to the distribution folder
def distTask = tasks.findByName('dist')
if (distTask) {
    android.testVariants.all { variant ->
        // Make the dist task depend on the test variant, so the test APK will be built
        distTask.dependsOn variant.assemble
        // TODO: remap the different test variants to different file names
    }
}
